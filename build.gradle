plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id "org.sonarqube" version "4.3.0.3225"
}

version = "${minecraft_version}-${mod_version}"
group = "${mod_group_id}"

base {
    archivesName = "${mod_archive_base}"
}

sonar {
    properties {
        property "sonar.projectKey", "GrowthcraftCE_Growthcraft-1.20"
        property "sonar.organization", "growthcraftce"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('========================================')
println('Java: ' + System.getProperty('java.version'))
println(' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ')')
println('Arch: ' + System.getProperty('os.arch'))
println("Build Version: ${mod_version}")
println('----------------------------------------')

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    copyIdeResources = true

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        // applies to all the run configs below
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', mod_id+',growthcraft_apiary'+',growthcraft_apples'+',growthcraft_bamboo'+',growthcraft_cellar'+',growthcraft_milk'+',growthcraft_rice', '--all', '--output', file('src/generated/resources/'),
            '--existing', file('src/main/resources/'), '--flat'
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    // Curse Forge
    maven { url "https://www.cursemaven.com" }
    // JEI, Tinker's Construct, Mantle, Iron Chests, Natura, etc.
    maven { url = "https://maven.blamejared.com/" }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Compile time mods for integration
    compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"))
    compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"))
    compileOnly fg.deobf("curse.maven:mekanism-${curseforge_mekanism}")
    compileOnly fg.deobf("curse.maven:immersive-engineering-${curseforge_immersive_engineering}")
    compileOnly fg.deobf("curse.maven:emi-580555:5497473")

    // Runtime mods for testing
    runtimeOnly fg.deobf("curse.maven:theoneprobe-${curseforge_theoneprobe}")
    runtimeOnly fg.deobf("curse.maven:patchouli-${curseforge_patchouli}")
    runtimeOnly fg.deobf("curse.maven:appleskin-${curseforge_appleskin}")
    runtimeOnly fg.deobf("curse.maven:mekanism-${curseforge_mekanism}")
    runtimeOnly fg.deobf("curse.maven:farmers-delight-${curseforge_farmers_delight}")
    runtimeOnly fg.deobf("curse.maven:immersive-engineering-${curseforge_immersive_engineering}")
    //runtimeOnly fg.deobf("curse.maven:serene-seasons-${curseforge_serene_seasons}")

    // Runtime mods for testing, pick one
    //runtimeOnly fg.deobf("curse.maven:jei-${curseforge_jei}")
    runtimeOnly fg.deobf("curse.maven:emi-580555:5497473")

    // Nice thing that shows the nbt in scrollable manner
    runtimeOnly fg.deobf("curse.maven:item-nbt-viewer-514135:4578522")
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version          : forge_version,
            forge_version_range    : forge_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

// Example for how to get properties into the manifest for reading at runtime.
tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                'Specification-Title'     : mod_id,
                'Specification-Vendor'    : mod_authors,
                'Specification-Version'   : '9',
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : mod_authors,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    // This is the preferred method to reobfuscate your jar file
    finalizedBy 'reobfJar'
}

// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing:
// tasks.named('publish').configure {
//     dependsOn 'reobfJar'
// }

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}
